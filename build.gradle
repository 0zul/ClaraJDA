apply plugin: 'java'
apply plugin: 'idea'

version = '0.3.22'
group = 'com.avairebot'
description = 'Orion Discord Bot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    moduleName = 'Orion'
}

tasks.withType(JavaCompile) {
    dependsOn 'clean'
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:-unchecked"
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Orion: Discord Boat',
            'Implementation-Version': version,
            'Main-Class': 'com.avairebot.orion.Main'
    }

    baseName = project.name

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError = false
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addStringOption('-html5')
}

repositories {
    mavenCentral()                     // main maven repo
    jcenter()                          // JDA and some other stuff
    mavenLocal()                       // useful for developing
    maven { url "https://jitpack.io" } // build projects directly from github
}

dependencies {
    compile group: 'net.dv8tion', name: 'JDA', version: '3.3.1_307'
    compile group: 'com.sedmelluq', name: 'lavaplayer', version: '1.2.45'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.1'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.6'
    compile group: 'ai.api', name: 'libai', version: '1.6.12'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.1'
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.7.1'
    compile group: 'io.prometheus', name: 'simpleclient', version: '0.1.0'
    compile group: 'io.prometheus', name: 'simpleclient_servlet', version: '0.1.0'
    compile group: 'io.prometheus', name: 'simpleclient_logback', version: '0.1.0'
    compile group: 'io.prometheus', name: 'simpleclient_hotspot', version: '0.1.0'
    compile group: 'com.vdurmont', name: 'emoji-java', version: '4.0.0'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'io.sentry', name: 'sentry-logback', version: '1.6.3'
    compile group: 'org.reflections', name: 'reflections', version: '0.9.11'
    compile group: 'com.udojava', name: 'EvalEx', version: '1.18'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching("**/orion.properties") {
        filter ReplaceTokens, tokens: [
            "project.version"   : project.version,
            "project.groupId"   : project.group,
            "project.artifactId": project.ext.moduleName
        ]
    }
}
